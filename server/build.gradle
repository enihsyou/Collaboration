plugins {
    id "org.springframework.boot" version "2.0.1.RELEASE"
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id "org.jetbrains.kotlin.jvm" version "1.2.40"
    id "org.jetbrains.kotlin.plugin.spring" version "1.2.40"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.2.40"
    id "org.jetbrains.dokka" version "0.9.16"
    id "war"
}

group = 'com.enihsyou.collaboration'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-mail')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-websocket')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation('org.springframework.session:spring-session-jdbc')
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    implementation('org.jetbrains.kotlin:kotlin-reflect')

    runtimeOnly('org.springframework.boot:spring-boot-devtools')
    runtimeOnly('org.postgresql:postgresql')
    runtimeOnly('mysql:mysql-connector-java')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')

    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
}

compileKotlin {
    kotlinOptions { freeCompilerArgs = ["-Xjsr305=strict"]; jvmTarget = "1.8" }
}

compileTestKotlin {
    kotlinOptions { freeCompilerArgs = ["-Xjsr305=strict"]; jvmTarget = "1.8" }
}

javadoc{
    options.encoding = "utf-8"
    options.locale = "zh_CN"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs/dokkaHtml"
    includeNonPublic = true
    skipEmptyPackages = false
    reportUndocumented = false

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/enihsyou/Collaboration/tree/master/server/src/main/kotlin"
        suffix = "#L"
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    includeNonPublic = true
    skipEmptyPackages = false
    reportUndocumented = false
    outputDirectory = "$buildDir/docs/dokkaJavadoc"
}

task dokkaHtmlAsJava(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'html-as-java'
    includeNonPublic = true
    skipEmptyPackages = false
    outputDirectory = "$buildDir/docs/dokkaHtmlAsJava"
}
